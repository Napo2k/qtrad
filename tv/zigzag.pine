// This Pine ScriptÂ® code is subject to the terms of the Mozilla Public License 2.0 at https://mozilla.org/MPL/2.0/
// Â© ChartPrime

//@version=6
indicator("ZigZag Volume Profile [ChartPrime]", overlay = true, max_lines_count = 500, max_labels_count = 245)

// --------------------------------------------------------------------------------------------------------------------
// ðŸ“Œ USER INPUTS
// --------------------------------------------------------------------------------------------------------------------

volumeProfilesQty = input.int(15, "Amount of ZigZag Volume Profiles to display", maxval = 20, minval = 1)

showSwingChannel     = input.bool(true, "Display", group = "Swing Channel")
swingLength          = input.int(150, "Length", group = "Swing Channel")
channelWidthFactor   = input.float(1, "Width", group = "Swing Channel", minval = 0.3, maxval = 2, step = 0.1)

enableVolumeProfile  = input.bool(true, "Display", group = "VolumeProfile")
volumeBinCount       = int(input.int(10, "Bins", maxval = 20, group = "VolumeProfile")/2)
volumebinWidth       = input.int(5, "Bins Width", maxval = 20, group = "VolumeProfile")
binColorLow          = input.color(color.lime, "", group = "VolumeProfile", inline = "1")
binColorHigh         = input.color(color.blue, "", group = "VolumeProfile", inline = "1")

showPOC              = input.bool(true, "Display", group = "PoC", inline = "poc")
pocLineWidth         = input.int(2, "", group = "PoC", inline = "poc")
pocLineColor         = input.color(color.red, "", group = "PoC", inline = "poc")

// --------------------------------------------------------------------------------------------------------------------
// ðŸ“Œ INTERNAL VARIABLES
// --------------------------------------------------------------------------------------------------------------------
totalVolumeBins       = array.new_float(500)
var pocLineArray      = array.new<line>()
var pocLineArray1     = array.new<line>()
var channelLineArray  = array.new<line>()
var profileLineArray  = array.new<line>()
var profileLabelArray = array.new<label>()
var ZigZagArray       = array.new<line>()


type swingProfile
    array<line> channel
    array<line> profile
    array<label> LBLprofile
    array<line> poc
    array<line> poc1
    array<line> zig

var SProfile = array.new<swingProfile>()

// --------------------------------------------------------------------------------------------------------------------
// ðŸ“Œ DRAWING FUNCTIONS
// --------------------------------------------------------------------------------------------------------------------
drawBinLevel(offset, index, startBar, startPrice, endBar, endPrice) =>
    yStart = startPrice + offset
    yEnd   = endPrice + offset

    if showSwingChannel
        channelLineArray.push(line.new(startBar, yStart, endBar, yEnd, color = color.new(chart.fg_color, 70)))

    slope = (yStart - yEnd) / (endBar - startBar)
    volumeAtLevel = 0.0

    for i = 0 to endBar - startBar
        level = endPrice + offset + slope * i
        if high[i] > level and low[i] < level
            volumeAtLevel += volume[i]

    totalVolumeBins.set(index, volumeAtLevel)
    [volumeAtLevel, slope]

drawVolumeBin(offset, vol, endBar, endPrice, slope, startBar, startPrice, trendDirection) =>
    range_ = endBar - startBar
    volumePercent = vol / totalVolumeBins.sum() * 100
    fillStart = startPrice + (trendDirection ? +slope * int(range_ / 100 * volumePercent) : -slope * int(range_ / 100 * volumePercent))
    barColor = vol == totalVolumeBins.max() ? pocLineColor : color.from_gradient(vol, 0, totalVolumeBins.max(), binColorHigh, binColorLow)

    if enableVolumeProfile
        profileLineArray.push(line.new(startBar + int(range_ / 100 * volumePercent), fillStart + offset, startBar, startPrice + offset, width = volumebinWidth, color = barColor, force_overlay = true))
        profileLabelArray.push(label.new(startBar, startPrice + offset, str.tostring(volumePercent, format.percent), style = label.style_label_right, textcolor = barColor, color = color(na), size = size.small, force_overlay = true))

    if vol == totalVolumeBins.max() and showPOC
        pocLineArray.push(line.new(endBar, endPrice + offset, endBar + 15, endPrice + offset, color = pocLineColor, width = pocLineWidth))
        pocLineArray1.push(line.new(startBar, startPrice + offset, endBar, endPrice + offset, color = pocLineColor, width = pocLineWidth))


// --------------------------------------------------------------------------------------------------------------------
// ðŸ“Œ ATR-BASED WIDTH AND SWING EXTREMES
// --------------------------------------------------------------------------------------------------------------------
atrRange        = ta.atr(200) * channelWidthFactor
swingHigh       = ta.highest(swingLength)
swingLow        = ta.lowest(swingLength)

var zigzagLine  = line(na)
var barIndexLow = int(na)
var priceLow    = float(na)
var barIndexHigh = int(na)
var priceHigh   = float(na)
var isBullish   = bool(na)

// --------------------------------------------------------------------------------------------------------------------
// ðŸ“Œ SWING DETECTION LOGIC
// --------------------------------------------------------------------------------------------------------------------
if swingHigh == high
    isBullish := true
if swingLow == low
    isBullish := false

if high[1] == swingHigh[1] and high < swingHigh
    barIndexHigh := bar_index[1]
    priceHigh := low[1]

if low[1] == swingLow[1] and low > swingLow
    barIndexLow := bar_index[1]
    priceLow := low[1]

// --------------------------------------------------------------------------------------------------------------------
// ðŸ“Œ PROFILE CREATION PER SWING
// --------------------------------------------------------------------------------------------------------------------
drawProfileSegment(startBar, startPrice, endBar, endPrice, direction) =>
    float binSlope = na
    for i = volumeBinCount to -volumeBinCount
        [vol, slope] = drawBinLevel(atrRange * i, i, startBar, startPrice, endBar, endPrice)
        binSlope := slope

    for i = volumeBinCount to -volumeBinCount
        if totalVolumeBins.size() > 0
            drawVolumeBin(atrRange * i, totalVolumeBins.get(i), endBar, endPrice, binSlope, startBar, startPrice, direction)

    SProfile.push(swingProfile.new(channelLineArray, profileLineArray, profileLabelArray, pocLineArray, pocLineArray1, ZigZagArray))

// --------------------------------------------------------------------------------------------------------------------
// ðŸ“Œ DRAW ZIGZAG & PROFILES
// --------------------------------------------------------------------------------------------------------------------
if isBullish != isBullish[1] and isBullish
    zigzagLine := line.new(barIndexLow, priceLow, barIndexHigh, priceHigh, force_overlay = true, width = 2)
    ZigZagArray.push(zigzagLine)

    drawProfileSegment(barIndexHigh, priceHigh, barIndexLow, priceLow, not isBullish)
    
if isBullish and priceHigh != priceHigh[1]
    zigzagLine.set_xy2(barIndexHigh, priceHigh)

if isBullish != isBullish[1] and not isBullish
    zigzagLine := line.new(barIndexHigh, priceHigh, barIndexLow, priceLow, force_overlay = true, width = 2)
    ZigZagArray.push(zigzagLine)

    drawProfileSegment(barIndexLow, priceLow, barIndexHigh, priceHigh, isBullish)

if not isBullish and priceLow != priceLow[1]
    zigzagLine.set_xy2(barIndexLow, priceLow)

// --------------------------------------------------------------------------------------------------------------------
// ðŸ“Œ EXTEND POC LINES INTO FUTURE
// --------------------------------------------------------------------------------------------------------------------
if pocLineArray.size() > 12
    poc = pocLineArray.shift()
    poc.delete()

if pocLineArray.size() > 0
    for line_ in pocLineArray
        line_.set_x2(bar_index + 15)
        if high > line_.get_y1() and low < line_.get_y1()
            line_.delete()
            pocLineArray.remove(pocLineArray.indexof(line_))


if SProfile.size() > volumeProfilesQty

    SP = SProfile.shift()

    for ch in SP.channel
        ch.delete()

    for pf in SP.profile
        pf.delete()

    for pfl in SP.LBLprofile
        pfl.delete()

    for pc in SP.poc
        pc.delete()

    for pc1 in SP.poc1
        pc1.delete()

    for zg in SP.zig
        zg.delete()
